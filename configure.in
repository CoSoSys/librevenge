AC_INIT(src/lib/WPXParser.cpp)

AM_CONFIG_HEADER(config.h)

dnl This next section is courtesy gtk+
dnl 
# Making releases:
#   WPD_MICRO_VERSION += 1;
#   WPD_INTERFACE_AGE += 1;
#   WPD_BINARY_AGE += 1;
# if any functions have been added, set WPD_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set WPD_BINARY_AGE and WPD_INTERFACE_AGE to 0.

WPD_MAJOR_VERSION=0
WPD_MINOR_VERSION=6
WPD_MICRO_VERSION=0

WPD_VERSION="$WPD_MAJOR_VERSION.$WPD_MINOR_VERSION.$WPD_MICRO_VERSION"

WPD_INTERFACE_AGE=0
WPD_BINARY_AGE=0

dnl # libtool versioning
dnl LT_RELEASE=$WPD_MAJOR_VERSION.$WPD_MINOR_VERSION
dnl AC_SUBST(LT_RELEASE)

LT_CURRENT=`expr $WPD_MICRO_VERSION - $WPD_INTERFACE_AGE`
LT_REVISION=$WPD_INTERFACE_AGE
LT_AGE=`expr $WPD_BINARY_AGE - $WPD_INTERFACE_AGE`
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

AM_INIT_AUTOMAKE(libwpd, $WPD_VERSION)

AC_PROG_CC

AC_PROG_CXX

AC_PROG_INSTALL

AC_DISABLE_STATIC
AC_PROG_LIBTOOL

AC_PATH_PROG(PKG_CONFIG, pkg-config, no)

PKG_CHECK_MODULES(LIBWPD,[
glib-2.0 >= 2.0.0
libgsf-1 >= 1.6.0
])

AC_ARG_ENABLE(debug,[  --enable-debug    Turn on debugging],[
	case "${enableval}" in
	   yes)	debug=true ;
		DEBUG_CXXFLAGS="-DDEBUG -g" ;;
	    no)	debug=false ;
		DEBUG_CXXFLAGS="-DNDEBUG" ;;
	     *)	AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
	esac
],[	debug=false
	DEBUG_CXXFLAGS="-DNDEBUG" 
])

# AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

LIBWPD_CXXFLAGS="$LIBWPD_CFLAGS"

AC_SUBST(LIBWPD_CXXFLAGS)
AC_SUBST(DEBUG_CXXFLAGS)
AC_SUBST(LIBWPD_LIBS)

AC_OUTPUT([
Makefile
src/Makefile
src/conv/Makefile
src/conv/html/Makefile
src/conv/raw/Makefile
src/conv/text/Makefile
src/lib/Makefile
libwpd-1.pc
libwpd.spec
])

case "$debug" in
     "true" )
        AC_MSG_RESULT([Configured with debugging symbols.]) ;;
esac
